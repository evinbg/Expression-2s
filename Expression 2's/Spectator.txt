# Description: Auto wirelinks a wire camera controller to allow user to spectate players

@name Spectator
@inputs 
@persist I P Target:entity CamEnt:entity Cam:wirelink
@outputs On 
@trigger 

if(first()){
    On = 0
    Target = entity()
    findIncludePlayerProps(owner())
    findByClass("gmod_wire_cameracontroller")
    findSortByDistance(entity():pos())
    
    printColor(vec(255,0,255),"[Spec] ",vec(255,255,255),"Commands:
- /spec <name>
- Press P to toggle on and off")

    if(find()){
        CamEnt = findResult(1)
        Cam = CamEnt:wirelink()
        printColor(vec(255,0,255),"[Spec] ",vec(0,255,0),"Camera controller found")
    }
    else{
        timer("check",10000)
        printColor(vec(255,0,255),"[Spec] ",vec(255,0,0),"Camera controller not found. Spawn a wire camera controller and it will auto link. Checking again in 10 seconds...")
    }
}

interval(10)
runOnChat(1)

LS = owner():lastSaid():lower():explode(" ")

P = owner():keyPressed("p")

Cam["Activated",number] = On
Cam["Position",vector] = Target:pos() + vec(0,0,Target:height() * 1.5)

if(clk("check")){
    findIncludePlayerProps(owner())
    findByClass("gmod_wire_cameracontroller")
    findSortByDistance(entity():pos())
    if(find()){
        CamEnt = findResult(1)
        Cam = CamEnt:wirelink()
        printColor(vec(255,0,255),"[Spec] ",vec(0,255,0),"Camera controller found")
    }
    else{
        timer("check",10000)
        printColor(vec(255,0,255),"[Spec] ",vec(255,0,0),"Camera controller not found. Spawn a wire camera controller and it will auto link. Checking again in 10 seconds...")
    }
}

if(changed(P) & P == 1){
    I++
}

if(changed(I) & I == 1){
    On = 1
    printColor(vec(255,0,255),"[Spec] ",vec(0,255,0),"On")
}

if(changed(I) & I == 2){
    On = 0
    printColor(vec(255,0,255),"[Spec] ",vec(255,0,0),"Off")
    I = 0
}

if(chatClk(owner()) & LS[1,string] == "/spec"){
    hideChat(1)
    TargetTemp = findPlayerByName(LS[2,string])
    if(TargetTemp:isPlayer()){
        Target = TargetTemp
        On = 1
        I = 1
        printColor(vec(255,0,255),"[Spec] ",vec(255,255,255),"Now spectating "+Target:name()+"")
    }
    else{
        printColor(vec(255,0,255),"[Spec] ",vec(255,0,0),"Couldn't find a player named \""+LS[2,string]+"\"")
    }
}
