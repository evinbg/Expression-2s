# Description: Creates a holo that you can configure that follows a target

@name Follower
@inputs 
@outputs 
@persist Model:string [Target X TempTarget CamEnt]:entity
@persist Distance Anim Speed Follow Float I C Scale Running SpeedConst On 
@trigger 

#[
/t <player>: The holo targets the specific player
/follow: The model follows you wherever the target goes
/unfollow: The model stops following the target
/speed <number>: Sets speed at which the holo follows or orbits the target
/model <model string>: Sets the model of the holo
/float: Orbits the holo around the target
/anim <number>: Plays specific animations
/stopanim: Stops the animations of the holo
/scale <number>: Changes the scale of the holo
]#

if(first()){
    printColor(vec(255,0,255),"Commands:\n",vec(255,255,255),"Check E2 or console")
    
    print(_HUD_PRINTCONSOLE,"Commands:
    /t <player>
    /follow
    /unfollow
    /speed <number>
    /model <model string>
    /float
    /anim <number>
    /stopanim
    /scale <number>")
    
    Target = owner()
    TempTarget = owner()
    Model = "models/Humans/Group01/Female_01.mdl"
    Anim = 3
    Speed = 12
    SpeedConst = Speed
    Follow = 1
    Float = 0
    Scale = 1
    On = 0
    holoCreate(1)
    holoModel(1,Model)
    holoScale(1,vec(Scale,Scale,Scale))
    holoEntity(1):animate(Anim,1)
    entity():setMaterial("models/debug/debugwhite")
}

interval(50)
runOnChat(1)
runOnKeys(Target,1)

LS = owner():lastSaid():lower():explode(" ")
Ang = (Target:pos() - holoEntity(1):pos()):toAngle()
Dist = (Target:pos():distance(holoEntity(1):pos()))
I++
Length = 5
Sin = sin(curtime() * 10 * Speed) * Length
Cos = cos(curtime() * 10 * Speed) * Length
C++
Color = hsv2rgb(vec(C * Speed / 2,1,1))
entity():setColor(Color)

if(C == 720){
    C = 0
}

if(chatClk(owner())){
    if(LS[1,string] == "/t"){
        hideChat(1)
        TempTarget = findPlayerByName(LS[2,string])
        if(TempTarget:isPlayer() == 1){
            Target = TempTarget
            printColor(vec(0,255,0),"Targeting: ",vec(255,255,255),""+Target:name()+"")
        }
        if(TempTarget:isPlayer() == 0){
            printColor(vec(255,0,0),"Couldn't find a player")
        }
    }
}

if(Follow == 1 & Float == 0 & Target:isPlayer()){
    if(Target:keyPressed("lshift")){
        Running = 1
    }else{
        Running = 0
    }
    if(changed(Running) & Running == 1){
        Speed = Speed * 1.66
    }
    if(changed(Running) & Running == 0){
        Speed = SpeedConst
    }
    if(Target:pos():distance(holoEntity(1):pos()) >= 100){
        holoAng(1,(Ang))
        holoPos(1,holoEntity(1):toWorld(vec(Speed,0,0)))
        Distance = 1
        Anim = 11
    }
    else{
        Anim = 3
        holoEntity(1):animate(Anim,1)
        Distance = 0
    }
    if(changed(Distance) & Distance == 1){
        holoEntity(1):animate(Anim,Speed / 6)
    }
}

if(Float == 1 & Follow == 0){
    holoAng(1,Target:toWorld(ang(30,I * SpeedConst / 2,0)))
    holoPos(1,Target:toWorld(vec(Cos * 8,Sin * 8,(Sin * Cos) + 60)))
}

if(chatClk(owner())){
    if(LS[1,string] == "/follow"){
        hideChat(1)
        Follow = 1
        Float = 0
        printColor(vec(0,255,0),"Following: ",vec(255,255,255),""+Target:name()+"")
    }
    if(LS[1,string] == "/unfollow"){
        hideChat(1)
        Follow = 0
        Float = 0
        printColor(vec(255,0,0),"Stopped Following")
    }
    if(LS[1,string] == "/speed"){
        hideChat(1)
        SpeedConst = LS[2,string]:toNumber()
        Speed = LS[2,string]:toNumber()
        printColor(vec(0,0,255),"Speed: ",vec(255,255,255),""+Speed+"")
    }
    if(LS[1,string] == "/model"){
        hideChat(1)
        Model = LS[2,string]
        holoModel(1,Model)
        printColor(vec(0,0,255),"Model: ",vec(255,255,255),""+Model+"")
    }
    if(LS[1,string] == "/float"){
        hideChat(1)
        Float = 1
        Follow = 0
        Anim = 9
        holoEntity(1):animate(Anim,1)
        printColor(vec(0,0,255),"Floating")
    }
    if(LS[1,string] == "/anim"){
        hideChat(1)
        Float = 0
        Follow = 0
        Anim = LS[2,string]:toNumber()
        holoEntity(1):animate(Anim,1)
        printColor(vec(0,0,255),"Animation: ",vec(255,255,255),""+Anim+"")
    }
    if(LS[1,string] == "/stopanim"){
        hideChat(1)
        Follow = 0
        Anim = 0
        holoEntity(1):animate(Anim,1)
        printColor(vec(255,0,0),"Stopped Animations")
    }
    if(LS[1,string] == "/scale"){
        Scale = LS[2,string]:toNumber()
        hideChat(1)
        holoScale(1,vec(Scale,Scale,Scale))
        printColor(vec(0,0,255),"Scale: ",vec(255,255,255),""+Scale+"")
    }
}
